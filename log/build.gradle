apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'jacoco'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def packageName = 'net.mm2d'
def libraryId = 'log'
def siteUrl = 'https://github.com/ohmae/Log'
def githubUrl = 'https://github.com/ohmae/Log'

group packageName
archivesBaseName = libraryId
version "${versionMajor}.${versionMinor}.${versionPatch}"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation 'junit:junit:4.12'
    testImplementation "io.mockk:mockk:1.9"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.group + '.' + libraryId
        licenses = ['MIT']
        websiteUrl = siteUrl
        issueTrackerUrl = githubUrl + '/blob/master/LICENSE'
        vcsUrl = githubUrl + '.git'
        issueTrackerUrl = githubUrl + '/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name libraryId
            url siteUrl

            packaging 'jar'
            groupId project.group
            artifactId libraryId
            version project.version

            licenses {
                license {
                    name 'The MIT License'
                    url 'https://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                connection githubUrl + '.git'
                url githubUrl
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'file:build/maven')
            pom.project {
                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

javadoc {
    title = "mmupnp $version API"
    description "Generates Javadoc."
    options.charSet = 'UTF-8'
    options.encoding = "UTF-8"
    options.links("http://docs.oracle.com/javase/jp/7/api/")
    options.stylesheetFile = new File("stylesheet.css")
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
